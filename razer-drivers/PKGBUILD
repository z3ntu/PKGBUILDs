# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Gabriele Musco <emaildigabry@gmail.com>

pkgbase=razer-drivers
pkgname=('python-razer' 'razer-daemon' 'razer-driver-dkms')
pkgver=1.1.2
pkgrel=1
pkgdesc="An entirely open source driver and user-space daemon that allows you to manage your Razer peripherals on GNU/Linux."
arch=('any')
url="https://github.com/terrycain/razer_drivers"
license=('GPL2')
makedepends=('make' 'python')
optdepends=('razercommander-git: gtk frontend for razer-drivers')
source=("$pkgbase-v$pkgver.tar.gz::https://github.com/terrycain/razer-drivers/archive/v$pkgver.tar.gz")
sha512sums=('9d9cfe3ac26662cc9a1618cb3cdc492d9629d48b6c6fd3de8bb1d5f802543116c498aee9370e703dabb7cacd15a18912ac151b9ef9cb280d1256a41903737ff7')

package_python-razer() {
  _pythondir=$(python3 -c 'import sys; print(sys.path[-1])')
  _srcpkgdir="$srcdir/$pkgbase-$pkgver"
  pkgdesc="A python library for controlling razer-daemon"
  depends=('razer-daemon' 'python' 'python-dbus' 'python-numpy')
  install=$pkgname.install

  mkdir -p $pkgdir/$_pythondir

  cp -r $_srcpkgdir/pylib/razer $pkgdir/$_pythondir/
}

package_razer-daemon() {
  _pythondir=$(python3 -c 'import sys; print(sys.path[-1])')
  _srcpkgdir="$srcdir/$pkgbase-$pkgver"
  pkgdesc="A daemon for controlling razer-driver"
  depends=('razer-driver-dkms' 'python-dbus' 'python-gobject' 'python-setproctitle' 'xautomation' 'xdotool' 'libdbus' 'python-notify2')
  install=$pkgname.install
  
  # use the make file instead of copying manually? maybe, not now.
  mkdir -p $pkgdir/usr/share/razer-service
  mkdir -p $pkgdir/usr/share/man/man5
  mkdir -p $pkgdir/usr/share/man/man8
  mkdir -p $pkgdir/$_pythondir
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/etc/xdg/autostart

  cp -r $_srcpkgdir/daemon/razer_daemon $pkgdir/$_pythondir/
  cp $_srcpkgdir/daemon/run_razer_daemon.py $pkgdir/usr/bin/razer-service
  cp $_srcpkgdir/daemon/resources/razer.conf $pkgdir/usr/share/razer-service/razer.conf.example

  cp -v $_srcpkgdir/install_files/desktop/razer-service.desktop $pkgdir/etc/xdg/autostart/razer-service.desktop
  
  gzip -c $_srcpkgdir/daemon/resources/man/razer.conf.5 > $pkgdir/usr/share/man/man5/razer.conf.5.gz
  gzip -c $_srcpkgdir/daemon/resources/man/razer-service.8 > $pkgdir/usr/share/man/man8/razer-service.8.gz
}

package_razer-driver-dkms() {
  _srcpkgdir="$srcdir/$pkgbase-$pkgver"
  pkgdesc="An entirely open source driver for managing Razer peripherals on Linux. (DKMS)"
  depends=('dkms' 'udev')
  install=$pkgname.install
  
  #part1: dkms driver
  mkdir -p $pkgdir/usr/src/$pkgbase-$pkgver
  mkdir -p $pkgdir/usr/lib/udev/rules.d

  cp -r $_srcpkgdir/driver $pkgdir/usr/src/$pkgbase-$pkgver
  cp $_srcpkgdir/Makefile $pkgdir/usr/src/$pkgbase-$pkgver
  cp $_srcpkgdir/install_files/dkms/dkms.conf $pkgdir/usr/src/$pkgbase-$pkgver
  
  #part2: udev rules
  cp $_srcpkgdir/install_files/udev/99-razer.rules $pkgdir/usr/lib/udev/rules.d
  cp $_srcpkgdir/install_files/udev/razer_mount $pkgdir/usr/lib/udev
}

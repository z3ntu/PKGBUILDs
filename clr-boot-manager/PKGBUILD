# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=clr-boot-manager
pkgver=1.5.4
pkgrel=1
pkgdesc="Kernel & Boot Loader Management"
arch=('i686' 'x86_64')
url="https://github.com/clearlinux/clr-boot-manager"
license=('LGPL-2.1')
source=("https://github.com/clearlinux/clr-boot-manager/releases/download/v1.5.4/clr-boot-manager-1.5.4.tar.xz"{,.asc})
md5sums=('8538185b1191469cedd8190a13718747'
         'SKIP')
validpgpkeys=('BFAA59E8B49115BB013A14BDB8987752EC6A08C9') # Ikey Doherty <michael.i.doherty@intel.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --with-vendor-prefix=ArchLinux \
    --with-kernel-modules-dir=/usr/lib/modules \
    --with-kernel-namespace=org.archlinux \
    --with-bootloader=systemd-boot
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

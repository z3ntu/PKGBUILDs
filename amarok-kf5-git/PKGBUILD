# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: MaÃ«l Kerbiriou <mael.kerbiriou-at-free-dot-fr>

pkgname=amarok-kf5-git
pkgver=2.8.90.r176.gc9b5bbb727
pkgrel=1
pkgdesc="The powerful music player for KDE - KF5 GIT version"
arch=("i686" "x86_64")
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=(kdelibs4support threadweaver kcmutils libmariadbclient knewstuff ktexteditor ffmpeg libofa taglib-extras)
makedepends=(git cmake extra-cmake-modules qt5-base qt5-svg qt5-script qt5-declarative qt5-webkit
            kcoreaddons solid kdelibs4support kdesignerplugin kdoctools kcodecs threadweaver plasma-framework ktexteditor
            kcmutils knewstuff knotifyconfig kdnssd taglib libmariadbclient
            taglib-extras qca-qt5 liblastfm ffmpeg qjson libgpod gdk-pixbuf2 libmtp loudmouth libofa)
#optdepends=("libgpod: support for Apple iPod audio devices"
#            "libmtp: support for portable media devices"
#            "loudmouth: backend needed by mp3tunes for syncing"
#            "ifuse: support for Apple iPod Touch and iPhone"
#            "libmygpo-qt: gpodder.net Internet Service"
#            "clamz: allow to download songs from Amazon.com")
conflicts=('amarok')
provides=('amarok')
source=("git+https://anongit.kde.org/amarok.git#branch=kf5")
sha512sums=('SKIP')

pkgver() {
    cd amarok
    git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    rm -rf build
    mkdir -p build
}

build() {
    cd build
    cmake ../amarok -Wno-dev \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DKDE_INSTALL_LIBDIR=lib

    make
}

package(){
    cd build
    make DESTDIR="$pkgdir" install
}

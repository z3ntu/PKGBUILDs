# $Id: PKGBUILD 225229 2017-04-25 02:43:42Z anatolik $
# Maintainer: Anatol Pomozov
# Contributor: 謝致邦 <Yeking@Red54.com>
# Contributor: Alucryd <alucryd at gmail dot com>

pkgname=android-tools
pkgver=5.1.1_r38
pkgrel=1
pkgdesc='Android platform tools'
arch=(armv7h i686 x86_64)
url='http://tools.android.com/'
license=(Apache MIT)
depends=(openssl pcre)
optdepends=('python: for mkbootimg script')
makedepends=(git clang gtest)
source=(git+https://android.googlesource.com/platform/hardware/libhardware#tag=android-$pkgver
        git+https://android.googlesource.com/platform/system/core#tag=android-$pkgver
        git+https://android.googlesource.com/platform/system/extras#tag=android-$pkgver
        git+https://android.googlesource.com/platform/external/libselinux#tag=android-$pkgver
#        git+https://android.googlesource.com/platform/external/f2fs-tools#tag=android-$pkgver
#        git+https://android.googlesource.com/platform/external/minijail#tag=android-$pkgver
        AndroidConfig.h
        build.sh # regenerate this file with generate_build.rb tool
        bash_completion.fastboot
        bash_completion.adb
        remove-selinux-android.patch
        use-capability.patch
        use-local-socket.patch
        ppc64el-ftbfs.patch
        preserve-ownership.patch
        remove-bionic-android.patch
        define-shell-command.patch
        fix-implicit-declaration-function.patch
        adb_libssl_11.diff
        adb_libssl_bc.diff
        ) 
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          '4f71622ebd4709a2836d38b9332fcb1da943678f'
          '6f2b810689533f75e285311f1cafddde1fdc0386'
          '7004dbd0c193668827174880de6f8434de8ceaee'
          '2e69152091bb9642be058e49ec6cb720a2fd91dc'
          '04cf4aa623a87581452740c6d92ac62fe60c9b5a'
          '7f144d6d709f6eaa3c7f7332c41c44e969a5572e'
          '65c2afba2006fdd212d7a99568abc018aff02d7e'
          'cda9d8fe25acc8d9e03978a1d774362dc867629f'
          'fbb6cad258bca4e9a07681f2e3718ac49ecff2ae'
          'c4b41ba90744c6ec866291e82d48244c843d1b78'
          '1c99636b16d129fa47a362c96a6a07178cb1fbc2'
          'bf21400341728ca62960d0da1fabe9d820abeae9'
          '3083d2586e2d9bd657b79166b174f3cc81b5943e'
          '995ea7a6e13e3f412ee2ab3601997935d5ada9ef')

prepare() {
  mkdir -p "$srcdir/hardware"
  mkdir -p "$srcdir/external"
  mkdir -p "$srcdir/build/core/combo/include/arch/linux-arm/"
  ln -sf "$srcdir/libhardware" "$srcdir/hardware/"
  ln -sf "$srcdir/libselinux" "$srcdir/external/"
  ln -sf "$srcdir/AndroidConfig.h" "$srcdir/build/core/combo/include/arch/linux-arm/"
  patch -Np2 -i remove-selinux-android.patch
  patch -Np2 -i use-capability.patch
  patch -Np2 -i use-local-socket.patch
  patch -Np2 -i ppc64el-ftbfs.patch
  patch -Np2 -i preserve-ownership.patch
  patch -Np2 -i remove-bionic-android.patch
  patch -Np2 -i define-shell-command.patch
  patch -Np2 -i fix-implicit-declaration-function.patch
  patch -Np2 -i adb_libssl_11.diff
  patch -Np2 -i adb_libssl_bc.diff
}

build() {
  PKGVER=$pkgver sh ./build.sh
}

package(){
  install -m755 -d "$pkgdir"/usr/bin
  install -m755 -t "$pkgdir"/usr/bin fastboot adb core/mkbootimg/mkbootimg
  install -Dm 644 bash_completion.fastboot "$pkgdir"/usr/share/bash-completion/completions/fastboot
  #adb completion is provided by bash-completion file now
  #install -Dm 644 bash_completion.adb "$pkgdir"/usr/share/bash-completion/completions/adb
}

# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=pmos-matrix-bot-git
_pkgname=matrix-bot
pkgver=r17.e469888
pkgrel=1
pkgdesc="Friendly bot that does not try to destroy all humans"
arch=('x86_64')
url="https://gitlab.com/postmarketOS/matrix-bot"
license=('GPL')
makedepends=('git' 'go-pie')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=('etc/conf.d/pmos-matrix-bot')
source=('git+https://gitlab.com/postmarketOS/matrix-bot.git'
        'pmos-matrix-bot.service'
        'pmos-matrix-bot.conf')
sha512sums=('SKIP'
            '2390052f2d116a00bfa98b377a0f3e2195a9f48dff130729afe8ff39cc893b8f983f42f91a12d251ee27a417f5bc1e01c95d78410943a4ac337a63944dcc1c5e'
            '90b308082d088bdb57ca559b8d52af5ff62dc16f47e1c63fe5359e76656498a4012f1989d19f6d32e3eae04c2acfe246858c19ef45f21d30c92d45a8b7d7ffb2')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "-extldflags $LDFLAGS" \
    -o pmos-matrix-bot .
}

package() {
  cd "$_pkgname"
  install -Dm755 pmos-matrix-bot "$pkgdir"/usr/bin/pmos-matrix-bot

  cd "$srcdir"
  install -Dm644 pmos-matrix-bot.service "$pkgdir"/usr/lib/systemd/system/pmos-matrix-bot.service
  install -Dm600 pmos-matrix-bot.conf "$pkgdir"/etc/conf.d/pmos-matrix-bot
}

# Maintainer: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=pmos-matrix-bot-git
_pkgname=matrix-bot
pkgver=r15.4e0ba7e
pkgrel=1
pkgdesc="Friendly bot that does not try to destroy all humans"
arch=('x86_64')
url="https://gitlab.com/postmarketOS/matrix-bot"
license=('GPL')
makedepends=('git' 'go-pie')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=('etc/conf.d/pmos-matrix-bot')
source=('git+https://gitlab.com/postmarketOS/matrix-bot.git'
        'pmos-matrix-bot.service'
        'pmos-matrix-bot.conf')
sha512sums=('SKIP'
            'c561306a6493a6bb51604b974f722c6f52ac2ef7ed548a1b28d8d7c2a60827c448d10f832daabf2aaa6f274bcee51a6dfbfba955c9d8696c8125a22315081c5a'
            '9b8f258969e4ce61ea256dd12a74984a58092561f3d69c6d9fa1556e1b6f8627363dd71a00c30e6d54d20980fa9ad26b0e6050ecb7cfeae16587788ab6d96b14')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "-extldflags $LDFLAGS" \
    -o pmos-matrix-bot .
}

package() {
  cd "$_pkgname"
  install -Dm755 pmos-matrix-bot "$pkgdir"/usr/bin/pmos-matrix-bot

  cd "$srcdir"
  install -Dm644 pmos-matrix-bot.service "$pkgdir"/usr/lib/systemd/system/pmos-matrix-bot.service
  install -Dm600 pmos-matrix-bot.conf "$pkgdir"/etc/conf.d/pmos-matrix-bot
}
